buildscript {
    apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.6.0/dependencies.gradle'
    ext {
        springBootVersion = springBootVersion
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.github.ben-manes:gradle-versions-plugin:0.21.0")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.ben-manes.versions'

group = 'no.fint'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    repositories {
        maven {
            url "http://dl.bintray.com/fint/maven"
        }
    }
    mavenLocal()
}


dependencies {
    compile("no.fint:fint-okonomi-model-java:${apiVersion}")
    compile('no.fint:fint-utdanning-resource-model-java:3.4.0')
    compile('no.fint:fint-oauth-token-service:1.4.0')
    compile('no.fint:fint-betaling-model:1.0.0-beta-1')

    compile('org.apache.commons:commons-lang3:3.9')
    compile('org.jooq:jool-java-8:0.9.14')
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-cache')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-data-mongodb:${springBootVersion}")
    compile('org.springframework.plugin:spring-plugin-core')
    compile('com.fasterxml.jackson.core:jackson-databind')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    compile("com.github.springfox.loader:springfox-loader:${springfoxLoaderVersion}")
    compileOnly("org.projectlombok:lombok:${lombokVersion}")

    runtime('org.springframework.boot:spring-boot-actuator')

    testCompile('no.fint:fint-test-utils:0.0.4')
    testCompile("cglib:cglib-nodep:${cglibVersion}")
    testCompile("org.spockframework:spock-spring:${spockSpringVersion}")
    testCompile("org.spockframework:spock-core:${spockSpringVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.betaling.Application",
                "Specification-Version": "${version}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

bootRepackage.enabled = false

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.6.0/dependencyReport.gradle'
